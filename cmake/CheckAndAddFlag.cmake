include ( CheckCXXCompilerFlag )
include ( CMakePushCheckState )
function ( checkandaddflag flag addto )
  set ( NAME "accepts_flag_${flag}" )
  string ( MAKE_C_IDENTIFIER ${NAME} NAME )
  string ( REPLACE "__" "_" NAME ${NAME} )
  string ( REPLACE "__" "_" NAME ${NAME} )
  if ( NOT DEFINED ${NAME} )
    cmake_push_check_state ( RESET )
    string ( REGEX REPLACE "^-Wno-" "-W" TESTFLAG ${flag} )
    set ( CMAKE_REQUIRED_FLAGS "${${addto}} ${TESTFLAG}" )
    set ( CMAKE_REQUIRED_QUIET true )
    message ( STATUS "Checking if compiler supports ${flag}" )
    check_cxx_source_compiles ( "
                              int main() { return 0; }
                              " "${NAME}" FAIL_REGEX "unknown warning option|is not supported|unrecognized command line option|but not for C\\\\+\\\\+" )
    if ( ${NAME} )
      message ( STATUS "Checking if compiler supports ${flag} - success" )
    else ()
      message ( STATUS "Checking if compiler supports ${flag} - failed" )
    endif ()
    cmake_pop_check_state ()
  endif ()
  if ( ${NAME} )
    set ( ${addto} "${${addto}} ${flag}" PARENT_SCOPE )
    list ( APPEND "${addto}_LIST" ${flag} )
    set ( "${addto}_LIST" ${${addto}_LIST} PARENT_SCOPE )
  endif ()
endfunction ()
